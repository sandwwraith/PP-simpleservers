group 'top.sandwwraith'
version '1.0'

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

buildscript {
    ext.kotlin_version = '1.1.0'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'

repositories {
    mavenCentral()
    maven {
        url 'https://dl.bintray.com/wasabifx/wasabifx/'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile 'org.wasabifx:wasabi:0.3.120'
}

def outputDir = new File(project.buildDir, 'output')

task copyAddons(type: Copy) {
    from 'process.cfg'
    into outputDir
}

//create a single Jar with all dependencies
task fatJar(type: Jar, dependsOn: 'copyAddons') {
    destinationDir = outputDir
    manifest {
        attributes 'Implementation-Version': version,
                'Main-Class': 'top.sandwwraith.simpleservers.App'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar

    //create runner file
    doLast {
        def f = new File(outputDir, 'run_process.sh')
        f.newWriter().withWriter {
            it << "#!/usr/bin/env bash\n"
            it << "java -jar $baseName-${version}.jar \$1"
        }
        f.executable = true
    }
}

defaultTasks 'clean', 'fatJar'
